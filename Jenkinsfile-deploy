pipeline {
    agent { label 'local' }
    parameters {
        booleanParam(name: 'DEPLOY', defaultValue: true, description: 'Enable deployment')
        choice(name: 'DEPLOY_TYPE', choices: ['kubernetes', 'docker'], description: 'Select deployment type')
        string(name: 'IMAGE_TAG', defaultValue: '', description: 'Image tag to deploy (leave empty to auto-detect from build)')
    }
    environment {
        // Only Jenkins-computed values here (before .env is loaded)
        IMAGE_TAG = "latest"
        KUBECONFIG = "${env.WORKSPACE}/kubeconfig"
    }
    stages {
        stage('Initialize') {
            steps {
                script {
                    echo "Current workspace path is: ${env.WORKSPACE}"
                }
            }
        }
        stage('Checkout') {
            steps {
                checkout scm
            }
        }
        stage('Setup Environment') {
            steps {
                script {
                    echo "Loading environment variables from Jenkins credentials..."
                    withCredentials([file(credentialsId: 'numerical_env_file', variable: 'ENV_FILE')]) {
                        // Remove existing .env file if it exists (handles read-only files)
                        sh 'rm -f .env || true'
                        
                        // Read the credentials file and write to .env using Jenkins API
                        def envContent = readFile(file: env.ENV_FILE)
                        writeFile(file: '.env', text: envContent)
                        echo "✅ Environment file created successfully"
                        
                        // Load environment variables from .env for use in pipeline
                        def envVars = sh(script: 'cat .env | grep -v "^#" | grep -v "^\$" | xargs', returnStdout: true).trim()
                        if (envVars) {
                            envVars.split().each { envVar ->
                                if (envVar.contains('=')) {
                                    def parts = envVar.split('=', 2)
                                    def key = parts[0]
                                    def value = parts.length > 1 ? parts[1] : ''
                                    if (key) {
                                        env."${key}" = value
                                    }
                                }
                            }
                        }
                        echo "✅ Environment variables loaded from .env"
                        
                        // Derive computed variables from base variables
                        env.BUILD_PROJECT_NAME = "${env.ENV_PROJECT_NAME}_build"
                        env.PROJECT_NAME_WITH_DASH = env.ENV_PROJECT_NAME.replace('_', '-')
                        // Extract base name for nginx conf (e.g., numerical -> numerical)
                        def baseName = env.ENV_PROJECT_NAME.split('_')[0]
                        env.NGINX_CONF_PATH = "/etc/nginx/sites-available/${baseName}"
                        
                        // Set registry path for Docker operations
                        env.REGISTRY = env.DOCKER_REGISTRY
                        env.REPOSITORY = "${env.DOCKER_REPOSITORY}/${env.DOCKER_IMAGE_NAME}"
                        
                        echo "Computed BUILD_PROJECT_NAME: ${env.BUILD_PROJECT_NAME}"
                        echo "Computed PROJECT_NAME_WITH_DASH: ${env.PROJECT_NAME_WITH_DASH}"
                        echo "Computed NGINX_CONF_PATH: ${env.NGINX_CONF_PATH}"
                        echo "Docker Image: ${env.REGISTRY}/${env.REPOSITORY}:${IMAGE_TAG}"
                    }
                }
            }
        }
        stage('Setup Kubernetes Config') {
            when {
                expression { return params.DEPLOY_TYPE == 'kubernetes' }
            }
            steps {
                script {
                    // Copy the kubeconfig file to the workspace
                    sh "sudo cp /root/.kube/config ${env.WORKSPACE}/kubeconfig"
                    // Change permissions of the kubeconfig file
                    sh "sudo chmod 644 ${env.WORKSPACE}/kubeconfig"
                }
            }
        }
        stage('Check & Create Nginx Configuration') {
            steps {
                script {
                    // Check if the Nginx configuration file exists
                    def configExists = sh(script: "test -f ${env.NGINX_CONF_PATH} && echo 'exists' || echo 'not exists'", returnStdout: true).trim()

                    if (configExists == 'not exists') {
                        echo "Nginx configuration file does not exist. Creating it now..."

                        // Create or overwrite the NGINX_CONF_PATH file with the content of nginx.conf using sudo tee
                        sh "sudo cat nginx.conf | sudo tee ${env.NGINX_CONF_PATH} > /dev/null"

                        // Replace placeholders in the configuration file
                        sh "sudo sed -i 's|SERVER_NAME|${env.SERVER_NAME}|g' ${env.NGINX_CONF_PATH}"
                        sh "sudo sed -i 's|DOCKER_PORT|${env.DOCKER_PORT}|g' ${env.NGINX_CONF_PATH}"

                        echo "Nginx configuration file created."

                        // Ensure Nginx is aware of the new configuration
                        sh "sudo ln -sf ${env.NGINX_CONF_PATH} /etc/nginx/sites-enabled/"
                    } else {
                        echo "Nginx configuration file already exists."
                    }                    
                }
            }
        }
        stage('Retrieve Image Tag from Build Job') {
            when {
                expression { params.DEPLOY}
            }
            agent { label 'local' } 
            steps {
                script {
                    echo "Retrieve image tag from ${env.BUILD_PROJECT_NAME}"

                    // Construct the API URL for the latest build
                    def api_url = "https://${env.JENKINS_DOMAIN}/job/${env.BUILD_PROJECT_NAME}/lastSuccessfulBuild/api/json"

                    // Log the API URL for debugging purposes
                    echo "Hitting API URL: ${api_url}"
                    
                    withCredentials([usernamePassword(credentialsId: 'jenkins-admin-credentials', usernameVariable: 'JENKINS_USER', passwordVariable: 'JENKINS_TOKEN')]) {
                        // Execute the curl command to retrieve the JSON response
                        // echo "usernameVariable: ${JENKINS_USER}, passwordVariable: ${JENKINS_TOKEN}"
                        def buildInfoJson = sh(script: "curl -u ${JENKINS_USER}:${JENKINS_TOKEN} ${api_url}", returnStdout: true).trim()

                        // Log the raw JSON response for debugging
                        echo "Raw JSON response: ${buildInfoJson}"

                        // Extract build number from "id" field (unique root-level field, avoids greedy match issue)
                        def imageTag = sh(script: """
                            echo '${buildInfoJson}' | grep -o '"id":"[0-9]*"' | head -1 | tr -dc '0-9'
                        """, returnStdout: true).trim()

                        echo "Retrieved image tag (build number): ${imageTag}"


                        // Check if REGISTRY, REPOSITORY, and imageTag are all defined and not empty
                        if (REGISTRY && REPOSITORY && imageTag) {
                            if (params.DEPLOY_TYPE == 'kubernetes') {
                                // Replace the placeholder in the deployment YAML
                                sh "sed -i 's|:latest|:${imageTag}|g' ${WORKSPACE}/deployment.yaml"
                            }   
                            
                            if (params.DEPLOY_TYPE == 'docker') {
                                // Ensure the correct image tag is used in the docker-compose.yml
                                sh """
                                sed -i 's|image: .*|image: ${REGISTRY}/${REPOSITORY}:${imageTag}|' docker-compose.yml
                                """
                            }
                        } else {
                            echo "One or more required variables (REGISTRY, REPOSITORY, imageTag) are not defined or empty. Skipping docker-compose.yml update."
                        }
                    }
                }
            }
        }

        stage('Deploy') {
            when {
                expression { params.DEPLOY }
            }
            steps {
                script {
                    if (params.DEPLOY_TYPE == 'docker') {

                        // First decommission Kubernetes if running
                        echo "Decommissioning Kubernetes deployment if exists..."
                        sh """
                            # Check if kubernetes deployment exists
                            if kubectl get deployment ${env.PROJECT_NAME_WITH_DASH}-app 2>/dev/null; then
                                echo "Found Kubernetes deployment. Scaling down..."
                                kubectl scale deployment ${env.PROJECT_NAME_WITH_DASH}-app --replicas=0 || true
                                echo "Kubernetes deployment scaled down"
                            else
                                echo "No Kubernetes deployment found"
                            fi
                        """

                        sh 'docker compose down'
                        sh 'docker compose pull'
                        sh 'docker compose up -d'

                        sleep 60

                        def containerRunning = sh(script: "docker ps -q -f name=${env.ENV_PROJECT_NAME}", returnStdout: true).trim()
                        if (!containerRunning) {
                            error "Container ${env.ENV_PROJECT_NAME} is not running"
                        } else {
                            echo "Container ${env.ENV_PROJECT_NAME} is running"
                            sh """
                                # Fetch HTTP status code with proper Host header
                                HTTP_STATUS=\$(curl -s -o /dev/null -w "%{http_code}" -H "Host: ${env.SERVER_NAME}" http://localhost:${env.DOCKER_PORT})
                                echo "HTTP Status: \$HTTP_STATUS"
                                
                                # Update Nginx configuration if status code is 200 or 302 (redirect is OK)
                                if [ "\$HTTP_STATUS" -eq 200 ] || [ "\$HTTP_STATUS" -eq 302 ]; then
                                    echo "✅ Docker service is healthy (HTTP \$HTTP_STATUS), updating Nginx..."
                                    sudo sed -i 's|proxy_pass .*;|proxy_pass http://0.0.0.0:${env.DOCKER_PORT};|' ${env.NGINX_CONF_PATH}
                                    sudo nginx -s reload
                                    echo "✅ Nginx configuration updated and reloaded successfully"
                                else
                                    echo "❌ Service not available (HTTP \$HTTP_STATUS). Nginx configuration not updated."
                                    exit 1
                                fi
                            """
                        }
                    } else if (params.DEPLOY_TYPE == 'kubernetes') {
                        
                        // First decommission Docker Compose if running
                        echo "Decommissioning Docker Compose services if running..."
                        sh """
                            # Check if docker compose services are running
                            if docker compose ps | grep -q ${env.ENV_PROJECT_NAME}; then
                                echo "Found Docker Compose services. Stopping them..."
                                docker compose down || true
                                echo "Docker Compose services stopped"
                            else
                                echo "No Docker Compose services found"
                            fi
                        """
                        
                        // Check if the file is copied successfully
                        if (fileExists("${env.WORKSPACE}/.env")) {
                            echo ".env file copied successfully."
                            
                            // Verify Kubernetes configuration
                            sh 'kubectl cluster-info'
                            
                            // Print current directory
                            sh 'pwd'
                            
                            // Delete existing secret if it exists
                            sh """
                            kubectl delete secret ${env.PROJECT_NAME_WITH_DASH}-secret || true
                            """

                            // Deploy to Kubernetes
                            sh """
                            kubectl create secret generic ${env.PROJECT_NAME_WITH_DASH}-secret --from-env-file=${WORKSPACE}/.env
                            kubectl apply -f ${WORKSPACE}/service.yaml
                            kubectl apply -f ${WORKSPACE}/deployment.yaml
                            """
                            
                            // Wait for a few seconds to let the app start
                            sleep 60

                            sh """
                                kubectl describe deployment/${env.PROJECT_NAME_WITH_DASH}-app
                            """

                            // Verify service and get NodePort
                            def nodePort = sh(script: "kubectl get service ${env.PROJECT_NAME_WITH_DASH}-service -o=jsonpath='{.spec.ports[0].nodePort}'", returnStdout: true).trim()
                            echo "Service NodePort: ${nodePort}"

                            // Get cluster IP address
                            def clusterIP = sh(script: "kubectl get nodes -o=jsonpath='{.items[0].status.addresses[0].address}'", returnStdout: true).trim()
                            echo "Cluster IP: ${clusterIP}"

                            // Health check with retry logic (5 attempts with 2-minute waits)
                            sh """
                                MAX_RETRIES=5
                                RETRY_COUNT=0
                                SUCCESS=false
                                WAIT_TIME=120
                                
                                echo "Starting health check for http://${clusterIP}:${nodePort}"
                                echo "Will retry \$MAX_RETRIES times with \$WAIT_TIME second waits"
                                
                                while [ \$RETRY_COUNT -lt \$MAX_RETRIES ]; do
                                    RETRY_COUNT=\$((RETRY_COUNT+1))
                                    echo "Attempt \$RETRY_COUNT/\$MAX_RETRIES..."
                                    
                                    HTTP_STATUS=\$(curl -s -o /dev/null -w "%{http_code}" --max-time 30 http://${clusterIP}:${nodePort} 2>/dev/null || echo "000")
                                    echo "HTTP Status: \$HTTP_STATUS"
                                    
                                    if [ "\$HTTP_STATUS" = "200" ] || [ "\$HTTP_STATUS" = "301" ] || [ "\$HTTP_STATUS" = "302" ]; then
                                        echo "✅ Service is reachable at http://${clusterIP}:${nodePort}"
                                        SUCCESS=true
                                        break
                                    else
                                        echo "❌ Service not ready (HTTP \$HTTP_STATUS)"
                                        echo "Checking pod status..."
                                        kubectl get pods -l app=${env.PROJECT_NAME_WITH_DASH} --no-headers | head -1
                                        
                                        if [ \$RETRY_COUNT -lt \$MAX_RETRIES ]; then
                                            echo "⏳ Waiting \$WAIT_TIME seconds before retry..."
                                            sleep \$WAIT_TIME
                                        fi
                                    fi
                                done
                                
                                if [ "\$SUCCESS" = "false" ]; then
                                    echo "ERROR: Service failed to become reachable after \$MAX_RETRIES attempts"
                                    echo "Debugging information:"
                                    kubectl get pods -l app=${env.PROJECT_NAME_WITH_DASH} || true
                                    kubectl logs -l app=${env.PROJECT_NAME_WITH_DASH} --tail=30 || true
                                    exit 1
                                fi
                                
                                # Update Nginx configuration
                                echo "Updating Nginx configuration at ${env.NGINX_CONF_PATH}..."
                                sudo sed -i 's|proxy_pass .*;|proxy_pass http://${clusterIP}:${nodePort};|' ${env.NGINX_CONF_PATH}
                                
                                if [ \$? -ne 0 ]; then
                                    echo "Failed to update Nginx configuration"
                                    exit 1
                                fi
                                
                                echo "Reloading Nginx..."
                                sudo nginx -s reload
                                
                                if [ \$? -ne 0 ]; then
                                    echo "Failed to reload Nginx"
                                    exit 1
                                fi
                                
                                echo "✅ Nginx reloaded successfully"
                            """
                        } else {
                            error ".env file not found in the workspace."
                        }
                    }
                    currentBuild.description = 'DEPLOYMENT_EXECUTED'
                }
            }
        }
        stage('Sentry release') {
            when {
                expression { params.DEPLOY }
            }
            steps {
                script {
                    echo "Sentry Release ..."
                    
                    // Use Jenkins credentials for Sentry auth token
                    withCredentials([string(credentialsId: 'sentry-auth-token', variable: 'SENTRY_AUTH_TOKEN')]) {
                        sh """
                            # Get the current git commit hash
                            VERSION=\$(git rev-parse HEAD)
                            
                            echo "Creating Sentry release for version: \$VERSION"

                            sentry-cli releases -o ${env.SENTRY_ORG} -p ${env.SENTRY_PROJECT} new \$VERSION

                            # Deploy the release
                            sentry-cli releases -o ${env.SENTRY_ORG} -p ${env.SENTRY_PROJECT} deploys \$VERSION new -e production
                            
                            echo "✅ Sentry release created successfully"
                        """
                    }
                }
            }
        }
    }
    post {
        success {
            script {
                // Retrieve the latest commit message
                if (currentBuild.description == 'DEPLOYMENT_EXECUTED') {
                    sh """curl -s \
                    -X POST \
                    --user $MAIL_JET_API_KEY:$MAIL_JET_API_SECRET \
                    https://api.mailjet.com/v3.1/send \
                    -H "Content-Type:application/json" \
                    -d '{
                        "Messages":[
                                {
                                        "From": {
                                                "Email": "$MAIL_JET_EMAIL_ADDRESS",
                                                "Name": "ArpanSahuOne Jenkins Notification"
                                        },
                                        "To": [
                                                {
                                                        "Email": "$MY_EMAIL_ADDRESS",
                                                        "Name": "Development Team"
                                                }
                                        ],
                                        "Subject": "Jenkins Build Pipeline your project ${currentBuild.fullDisplayName} Ran Successfully",
                                        "TextPart": "Hola Development Team, your project ${currentBuild.fullDisplayName} is now deployed",
                                        "HTMLPart": "<h3>Hola Development Team, your project ${currentBuild.fullDisplayName} is now deployed </h3> <br> <p> Build Url: ${env.BUILD_URL}  </p>"
                                }
                        ]
                    }'"""
                }

                

                // Trigger the common_readme job on success when last commit is not Automatic Update from common_readme
                def commitMessage = sh(script: "git log -1 --pretty=%B", returnStdout: true).trim()
                if (!commitMessage.contains("Automatic Update")) {
                    def expandedProjectUrl = "https://github.com/arpansahu/${env.ENV_PROJECT_NAME}"
                    build job: 'common_readme', parameters: [
                        string(name: 'project_git_url', value: expandedProjectUrl),
                        string(name: 'environment', value: 'prod')
                    ], wait: false
                } else {
                    echo "Skipping common_readme job trigger due to commit message: ${commitMessage}"
                }
            }
        }
        failure {
            sh """curl -s \
            -X POST \
            --user $MAIL_JET_API_KEY:$MAIL_JET_API_SECRET \
            https://api.mailjet.com/v3.1/send \
            -H "Content-Type:application/json" \
            -d '{
                "Messages":[
                        {
                                "From": {
                                        "Email": "$MAIL_JET_EMAIL_ADDRESS",
                                        "Name": "ArpanSahuOne Jenkins Notification"
                                },
                                "To": [
                                        {
                                                "Email": "$MY_EMAIL_ADDRESS",
                                                "Name": "Developer Team"
                                        }
                                ],
                            "Subject": "Jenkins Build Pipeline your project ${currentBuild.fullDisplayName} Ran Failed",
                            "TextPart": "Hola Development Team, your project ${currentBuild.fullDisplayName} deployment failed",
                            "HTMLPart": "<h3>Hola Development Team, your project ${currentBuild.fullDisplayName} is not deployed, Build Failed </h3> <br> <p> Build Url: ${env.BUILD_URL}  </p>"
                        }
                ]
            }'"""
        }
    }
}
